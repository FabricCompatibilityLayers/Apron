//file:noinspection GradlePackageVersionRange
plugins {
	id "java-library"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "babric-loom" version "1.1-SNAPSHOT" apply false
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def ENV = System.getenv()

version = rootProject.mod_version

def getSubprojectVersion(project) {
	return rootProject.version
}

def moduleDependencies(project, List<String> depNames) {
	def deps = depNames.iterator().collect { project.dependencies.project(path: ":$it", configuration: 'namedElements') }

	project.dependencies {
		deps.each {
			api it
		}
	}
}

allprojects {
	group = rootProject.maven_group

	apply plugin: "maven-publish"

	publishing {
		repositories {
			if (ENV.MAVEN_URL) {
				maven {
					name 'halotroop-blucobalt.dev'
					url 'https://repo.blucobalt.dev/repository/maven-halotroop'
					credentials {
						username ENV.MAVEN_USERNAME
						password ENV.MAVEN_PASSWORD
					}
				}
			} else {
				mavenLocal()
			}
		}
	}

	apply plugin: "java-library"
	//apply plugin: "checkstyle"
	apply plugin: "babric-loom"

	tasks.withType(JavaCompile).configureEach {
		it.options.release = project.name == "apron-stapi-fixes" ? 17 : 8
	}

	java {
		// Must be added before the split source sets are setup.
		withSourcesJar()
	}

	loom {
		gluedMinecraftJar()
		customMinecraftManifest.set("https://babric.github.io/manifest-polyfill/${minecraft_version}.json")
		enableTransitiveAccessWideners = true
	}

	repositories {
		mavenCentral()
		maven {
			name 'Babric'
			url 'https://maven.glass-launcher.net/babric'
			content {
				includeGroup 'babric'
				includeGroup 'org.lwjgl.lwjgl'
			}
		}
		maven {
			name 'Legacy Fabric'
			url "https://repo.legacyfabric.net/repository/legacyfabric/"
			content {
				includeGroup 'net.legacyfabric'
				includeGroup 'net.legacyfabric.legacy-fabric-api'
			}
		}

		maven {
			name 'StationAPI'
			url 'https://maven.glass-launcher.net/snapshots'
		}
		// Used for a StationAPI dependency.
		maven {
			name = 'Froge'
			url 'https://maven.minecraftforge.net/'
		}
		// Used for mappings.
		maven {
			name = 'Glass Releases'
			url = 'https://maven.glass-launcher.net/releases'
		}

		maven {
			name 'Modrinth'
			url 'https://api.modrinth.com/maven'
			content {
				includeGroup 'maven.modrinth'
			}
		}
		maven {
			name 'JitPack'
			url 'https://jitpack.io'
		}

		flatDir {
			dirs rootProject.rootDir.toString() + "/remapped/client"
			dirs rootProject.rootDir.toString() + "/remapped/server"
			dirs rootProject.rootDir.toString() + "/libs"
		}
	}

	allprojects.each { p ->
		loom.mods.register(p.name) {
			sourceSet p.sourceSets.main
		}
	}

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings "net.legacyfabric:beta-yarn:${rootProject.minecraft_version}+build.${rootProject.mappings}"
		modApi "babric:fabric-loader:${rootProject.loader_version}"

		modApi("net.legacyfabric.legacy-fabric-api:legacy-fabric-logger-api-v1:${rootProject.lfapi_version}")

		modApi("net.modificationstation:StationAPI:${rootProject.stapi_version}")

		modApi("com.github.paulevsGitch:BHAPI:${project.bhapi_version}")

//		modRuntimeOnly(group: "remapped.client", name: "mangopack")

		// Done
	//	modRuntimeOnly(group: "remapped.client", name: "modloader", version: "b1.7.3")
	//	modRuntimeOnly(group: "remapped.client", name: "modloadermp-1.7.3-unofficial", version: "v2")
	//	modRuntimeOnly(group: "remapped.server", name: "modloadermp-1.7.3-unofficial-server", version: "v2")
	//	modRuntimeOnly(group: "remapped.client", name: "reforged-client", version: "1.0.1")
	//	modRuntimeOnly(group: "remapped.client", name: "audiomod", version: "b1.7.3")
//		modRuntimeOnly(group: "remapped.client", name: "shockahpi", version: "r5.1")
	//	modRuntimeOnly(group: "remapped.client", name: "playerapi-1.7.3", version: "v1.7")
	//	modRuntimeOnly(group: "remapped.client", name: "itemspriteapi", version: "v1.2")
	// 	modRuntimeOnly(group: "remapped.client", name: "guiapi0.11.0", version: "1.7")
	//	modRuntimeOnly(group: "remapped.client", name: "modoptionsapi", version: "v0.7")
//		modRuntimeOnly(group: "remapped.client", name: "reforgedsapi-client")
		// Todo
	//	modRuntimeOnly(group: "remapped.server", name: "minecraftforge-server", version: "1.0.7-20110907")
	}

	tasks.withType(ProcessResources).configureEach {
		inputs.property "version", project.version

		filesMatching("fabric.mod.json") {
			expand "version": project.version
		}
	}

	//checkstyle {
	//	configFile = rootProject.file("checkstyle.xml")
	//	toolVersion = "9.1"
	//}

	tasks.withType(AbstractArchiveTask) {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}
}

subprojects {
	dependencies {
		if (project.name != "apron-impl") {
			api(project.dependencies.project(path: ":apron-impl", configuration: 'namedElements'))
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifact(remapJar) {
					builtBy(remapJar)
				}

				artifact(remapSourcesJar) {
					builtBy remapSourcesJar
				}
			}
		}
	}

	javadoc.enabled = false
}

subprojects.each {
	remapJar.dependsOn("${it.path}:remapJar")
}

dependencies {
	afterEvaluate {
		subprojects.each {
			api project(path: "${it.path}", configuration: "namedElements")
		}
	}
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
	from("FORGE_LICENSE")
	from("PAULSCODE_LICENSE")

	from("./original/client") { into("libs") }
	from("./original/server") { into("libs") }
}

remapJar {
	afterEvaluate {
		subprojects.each {
			// Include the jar from the sub project.
			nestedJars.from project("${it.path}").tasks.getByName("remapJar")
		}
	}
}
